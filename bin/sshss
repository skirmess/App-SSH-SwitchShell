#!/usr/bin/perl

# vim: ts=4 sts=4 sw=4 et: syntax=perl
#
# Copyright (c) 2017-2022 Sven Kirmess
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use 5.006;
use strict;
use warnings;

our $VERSION = '0.006';

package App::SSH::SwitchShell;

use Cwd        ();
use English    qw(-no_match_vars);
use File::Spec ();
use Getopt::Long;

main() if !caller;

sub main {
    my $home_arg;
    my @env;
    my @env_login;
    my @env_command;
    GetOptions(
        'h=s'           => \$home_arg,
        'e=s'           => \@env,
        'env-command=s' => \@env_command,
        'env-login=s'   => \@env_login,
    );

    # configure env variables
    configure_env(@env);
    if ( exists $ENV{SSH_ORIGINAL_COMMAND} ) {
        configure_env(@env_command);
    }
    else {
        configure_env(@env_login);
    }

    # configure the home directory
    if ( defined $home_arg ) {
        configure_home($home_arg);
    }

    # configure the shell
    my $shell = configure_shell();

    # Get the last component of the shell name.
    my $shell0 = ( File::Spec->splitpath($shell) )[2];

    # If we have no command, execute the shell. In this case, the shell
    # name to be passed in argv[0] is preceded by '-' to indicate that
    # this is a login shell.

    if ( !exists $ENV{SSH_ORIGINAL_COMMAND} ) {

        # Launch a login shell
        exec {$shell} "-$shell0";
    }
    else {
        # Execute the command using the user's shell. This uses the -c
        # option to execute the command.
        exec {$shell} $shell0, '-c', $ENV{SSH_ORIGINAL_COMMAND};
    }

    print {*STDERR} "$!: $shell0\n";
    exit 1;
}

sub configure_env {
    my (@env) = @_;

  ENV:
    for my $env (@env) {
        my ( $key, $value ) = split /=/, $env, 2;

        next ENV if !defined $key;
        if ( !defined $value ) {
            delete $ENV{$key};
        }
        else {
            $ENV{$key} = $value;
        }
    }

    return;
}

# Update the HOME env variable and change to the new home directory if it is
# different from the already configured HOME. Otherwise do nothing because
# SSH already ensures that HOME is configured correctly and we are chdird
# into it.
sub configure_home {
    my ($myhome) = @_;

    if ( !File::Spec->file_name_is_absolute($myhome) ) {
        $myhome = File::Spec->rel2abs($myhome);
    }

    if ( exists $ENV{HOME} && defined $ENV{HOME} && -d $ENV{HOME} ) {
        my $home = File::Spec->canonpath( $ENV{HOME} );

        my $home_rp   = Cwd::realpath($home);
        my $myhome_rp = Cwd::realpath($myhome);
        return if $home_rp eq $myhome_rp;
    }

    $ENV{HOME} = $myhome;

    if ( !chdir $myhome ) {
        print {*STDERR} "Could not chdir to home '$myhome': $!";
    }

    return;
}

sub configure_shell {
    my $shell = get_shell();

    # Make sure SHELL points to the correct shell, either the shell
    # specified as argument, the shell from the password file, or /bin/sh
    $ENV{SHELL} = $shell;

    return $shell;
}

sub get_shell {

    # The shell can be specified as argument
    if (@ARGV) {
        my $shell = shift @ARGV;

        if ( !File::Spec->file_name_is_absolute($shell) ) {
            print {*STDERR} "Shell '$shell' is not an absolute path\n";
        }
        elsif ( !-e $shell ) {
            print {*STDERR} "Shell '$shell' does not exist\n";
        }
        else {
            return $shell if -x $shell;

            print {*STDERR} "Shell '$shell' is not executable\n";
        }
    }

    # Get the shell from the password data. An empty shell field is
    # legal, and means /bin/sh.

    my $shell = ( getpwuid $EUID )[8];
    return $shell if defined $shell && $shell ne q{};
    return '/bin/sh';
}

1;

__END__

=pod

=encoding UTF-8

=head1 NAME

sshss - use your preferred shell and own home directory for shared SSH accounts

=head1 VERSION

Version 0.006

=head1 SYNOPSIS

=over

=item B<sshss> [-h <home>] [-e VARIABLE[=value]]...
    [--env-command VARIABLE[=value]]...
    [--env-login VARIABLE[=value]...  [shell]

=back

=head1 DESCRIPTION

B<sshss> adds support to ease the pain of these dreadful shared accounts
prevalent at some organizations. All you have to do is add B<sshss> to
the I<command> string of the F<authorized_keys> file. B<sshss> lets you
define a different shell then the one defined in the passwd database,
configure environment variables, and a different directory as your home
directory.

All features, the personal home directory, the environment variables,
and the shell change, can be used independently without using the other.

If you specify a new shell the shell is not only used as the login
shell but also if you directly run a command. This includes commands
that run over SSH like L<scp(1)|scp(1)> and L<rsync(1)|rsync(1)>. It's
your responsibility to not use an overly obscure shell that breaks these
commands.

The used shell must support the I<-c> flag to run a command, which is
used if you run a command directly over SSH, including L<scp(1)|scp(1)>
and L<rsync(1)|rsync(1)>. This is the default used by SSH itself. If
your shell would work with plain SSH, it will also work with B<sshss>.

B<sshss> tries to behave as much as possible like the C<do_child>
function from F<session.c> from OpenSSH portable.

B<sshss> uses no non-core modules.

=head1 OPTIONS

=over

=item I<-e VARIABLE[=value]>

Sets the environment variable C<VARIABLE> to C<value>.

If C<value> is an empty string, the environent variable is set to an
empty string.

If the C<=> sign is ommited, the variable is deleted

=item I<--env-command VARIABLE[=value]>

Same as C<-e> but this variable is only set for non-login sessions.

=item I<--env-login VARIABLE[=value]>

Same as C<-e> but this variable is only set for login sessions.

=item I<-h home>

Specifies the directory to set as your home directory. sshss will set
the I<HOME> environment variable and change to this directory.

If the argument is a relative directory it is made absolute from the
current working directory, which is the default home directory of the
account you log in to.

If this is not specified, or if it is the same as the already configured
home directory, nothing is changed.

Note: Symlinks are resolved to compare the directory with the current
defined home directory, but they are not resolved when setting the
C<HOME> variable.

=item I<shell>

Specifies the shell to be used instead of the one specified in the
passwd database.

This can be used to overwrite the shell configured for a shared account.
It can also be used to change the shell for your personal account if
your organization does not have a supported way to change your shell.

If the shell is omitted, B<sshss> uses the default shell for the account
from the passwd database.

If the specified shell is not an absolute path, B<sshss> uses the
default shell for the account from the passwd database.

=back

=head1 EXIT STATUS

B<sshss> exits 1 if an error occurs until it can exec the shell. After
the exec the exit status depends on the executed shell or the command
run in this shell.

=head1 EXAMPLES

=head2 B<Example 1> Change the shell to ksh93 and use a custom home directory

Create a directory to contain your own home directory. We create the
directory ~/.ryah in this example. Add the following command string in
front of your SSH key in the F<~/.ssh/authorized_keys> file:

  command="/usr/bin/env perl .ryah/.ssh/sshss -h .ryah /usr/bin/ksh93"

Note: Adjust the path to C<sshss> if you didn't put it in the F<.ssh>
directory in your new home directory.

When you login over SSH with your key to the admin account,

=over

=item * your shell will be F</usr/bin/ksh93>, started as login shell

=item * the SHELL environment variable will be set to F</usr/bin/ksh93>

=item * the HOME environment variable will be set to F</home/admin/.ryah>
(The shared accounts home directory is /home/admin in this example)

=item * the working directory will be F</home/admin/.ryah>
(The shared accounts home directory is /home/admin in this example)

=back

=head2 B<Example 2> Change the shell to ksh93 without changing the home directory

Add the B<sshss> script to e.g. the F<~/.ssh> directory or any other
directory.

Add the following command string in front of your SSH key in the
F<~/.ssh/authorized_keys> file:

  command="/usr/bin/env perl .ssh/sshss /usr/bin/ksh93"

Note: Adjust the path to C<sshss> if you didn't put it in the F<.ssh>
directory.

When you login over SSH with your key to the admin account,

=over

=item * your shell will be F</usr/bin/ksh93>, started as login shell

=item * the SHELL environment variable will be set to F</usr/bin/ksh93>

=back

=head2 B<Example 3> Use a custom home directory

Create a directory to contain your own home directory. We create the
directory ~/.ryah in this example. Add the following command string in
front of your SSH key in the F<~/.ssh/authorized_keys> file:

  command="/usr/bin/env perl .ryah/.ssh/sshss -h .ryah"

When you login over SSH with your key to the admin account,

=over

=item * your shell will be the shell defined in the passwd database,
started as login shell. If the shell specified in the passwd database is
empty or invalid, F</bin/sh> is used instead.

=item * the SHELL environment variable will be set to the shell defined
in the passwd database. If the shell specified in the passwd database is
empty or invalid, the SHELL environment variable is set to F</bin/sh>
instead.

=item * the HOME environment variable will be set to F</home/admin/.ryah>
(The shared accounts home directory is /home/admin in this example)

=item * the working directory will be F</home/admin/.ryah>
(The shared accounts home directory is /home/admin in this example)

=back

=head2 B<Example 4> Use a custom bash profile

Create your own F<.bash_profile> file and call it something like e.g.
F<.bash_profile.ryah>. Then add the following at the top of the
F<.bash_profile> file in the shared account.

  if [[ $SSHSS_USER = 'ryah' ]]
  then
      . $HOME/.bash_profile.$SSHSS_USER
      return
  fi

Create your own F<.bashrc> file and call it something like e.g.
F<.bashrc>. Then add the following at the top of the
F<.bashrc> file in the shared account.

  if [[ $SSHSS_USER = 'ryah' ]]
  then
      . $HOME/.bashrc.$SSHSS_USER
      return
  fi

Then add the following command to your F<authorized_keys> file:

  command="/usr/bin/env perl sshss -e SSHSS_USER=ryah"

The F<.bash_profile> file is executed when you log in to the system and
the F<.bashrc> file is only run when you start another shell after
logging in. Most of the time the F<.bashrc> file should be sourced from
the F<.bash_profile> file.

=head2 B<Example 5> Use a custom ksh93 profile

Create your own F<.profile> file and call it something like e.g.
F<.profile.ryah>. Then add the following at the top of the
F<.profile> file in the shared account.

  if [[ $ENV =~ /.kshrc.ryah$ ]]
  then
      . $HOME/.profile.ryah
      return
  fi

Then add the following command to your F<authorized_keys> file:

  command="/usr/bin/env perl sshss -e ENV=$HOME/.kshrc.ryah"

=head2 B<Example 6> Configure your Git user

One of the many problems of shared accounts is that the Git author is
most likely configured for someone else, or for the sared account.

Then add the following command to your F<authorized_keys> file to fix
this:

  command="/usr/bin/env perl sshss -e GIT_AUTHOR_NAME='Sven Kirmess' -e GIT_AUTHOR_EMAIL='sven@example.com' -e GIT_COMMITTER_NAME='Sven Kirmess' -e GIT_COMMITTER_EMAIL='sven@example.com'"

These variables get precedence over whatever is configured in the
shared accounts F<.gitconfig>.

=head1 ENVIRONMENT

=over

=item HOME

If the C<-h> option is used the C<HOME> environment variable is set to
the new home directory and the working directory is changed to this new
home directory.

Otherwise the HOME environment variable is not used, nor is the working
directory changed.

=item SHELL

The environment variable SHELL is set to the shell that is either used
as interactive shell or that is used to execute the command.

=back

=head1 SEE ALSO

L<passwd(4)|passwd(4)>, L<sshd(1)/AUTHORIZED_KEYS FILE FORMAT>

=head1 SUPPORT

=head2 Bugs / Feature Requests

Please report any bugs or feature requests through the issue tracker
at L<https://github.com/skirmess/App-SSH-SwitchShell/issues>.
You will be notified automatically of any progress on your issue.

=head2 Source Code

This is open source software. The code repository is available for
public review and contribution under the terms of the license.

L<https://github.com/skirmess/App-SSH-SwitchShell>

  git clone https://github.com/skirmess/App-SSH-SwitchShell.git

=head1 AUTHOR

Sven Kirmess <sven.kirmess@kzone.ch>

=cut
